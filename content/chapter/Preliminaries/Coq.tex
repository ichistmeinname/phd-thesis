\section{Programming with Dependent Types in Coq}
\label{sec:progDT}

In this section we give an introduction to programming with the interactive theorem prover Coq.
In order to write functional programs, Coq provides the specification language \emph{Gallina}.
More precisely, Gallina is a dependently typed functional programming language.
The theorem prover part of Coq is based on the calculus of inductive construction \citep{coquand1988inductively}, a derivative of the calculus of construction that was introduced by \citet{coquand1986calculus}.
As Coq is not an automatic theorem prover, it additionally provides a tactic language called \emph{Ltac} that enables the user to interactively construct proofs.

This introduction is structured as follows.
We first give an overview on writing functional programs in Coq including common obstacles with regard to Coq's totality restriction as well as how to overcome them.
As some of these solutions lead us to dependently typed programming, we take a look at how to formalise properties about programs and how to prove them.
In that light, we give a beginner\--friendly introduction on how to use Coq's tactic language to write proofs.
Note that we do not give a formal definition of the calculus of constructions or other concepts with regard to the implementation of Coq's logic, but suggest the interested reader to study other textbooks for a full\--blown introduction \citep{pierce2010software, chlipala2013certified}.
Instead we focus on how to use Coq as a tool to formalise and prove properties about programs.

\subsection{Functional Programming}

We can define a lot of common functions we know from functional programming languages like Haskell or Curry one\--to\--one in Coq.
As a code convention, we will start data type and constructor names with lower\--case letters as many standard types in Coq follow the same convention.
Consider the following data type definition for Peano numbers.

\begin{coqcode}
Inductive nat : Type :=
| z : nat
| s : nat -> nat.
\end{coqcode}

Note that the data type declaration for \cinl{nat} is annotated with \cinl{Type}.
Coq is build upon a sophisticated type hierarchy; for us it is enough to know that we will use \cinl{Type} for inductive data types and type synonyms.
The syntax to define constructors for data types resembles GADT\--style definitions in Haskell, that is, we specify the full type signature for each constructor, as we are used to for function definitions.
Furthermore, we list all constructors line\--by\--line and each line begins with a pipe \cinl{|} as delimiter.

Next, we define a function to check if a natural number \cinl{nat} is zero as well as a function for addition on natural numbers using pattern matching.
Note that both definitions have explicit type annotations for the argument and return types, although Coq could infer all of them.

\begin{coqcode}
Definition isZero (p : nat) : bool :=
  match p with
  | z  _ => true
  | s _ => false
  end.

Fixpoint add (p1 p2 : nat) : nat :=
  match p1 with
  | z   => p2
  | s p => s (add p p2)
  end.
\end{coqcode}

The pipe symbol \cinl{|} is again used as a delimiter: this time to list all patterns we want to distinguish in a \cinl{match ... with}\--expression that corresponds to Haskell's \hinl{case ... of}\--expressions.
Note that the definition of \cinl{isZero} uses the predefined \cinl{bool} type as result, and that we can group successive arguments of the same type in the type signature.
The function definitions above represent two different kinds of functions: \cinl{isZero} is a non\--recursive function, whereas \cinl{add} is a representative for recursive functions.
In Coq it is crucial to define recursive functions using the keyword \cinl{Fixpoint}.
Definitions that are marked as \cinl{Fixpoint}s need to pass an additional check: as a total language, all functions in Coq have to terminate.
In case of recursive functions, Coq checks if there is one argument that is structurally smaller than the original argument passed to the function for each recursive call.
In our example definition \cinl{add}, the argument \cinl{p} of the recursive call in the second case of the branch is structurally smaller than the original value \cinl{p1}, since the pattern match reifies \cinl{p1} to \cinl{(s p)}.

As an example for a recursive function that Coq's termination checker fails to accept, we try to define the following artificial definition that decreases on one of its arguments at each recursive call, but does not have one designated argument that decreases for each call.

\begin{coqcode}
Fail Fixpoint test (b : bool) (n m : nat) : bool :=
  match (n,m) with
  | (z,_)      => true
  | (_,z)      => false
  | (s p, s q) => if b then test b p m else test b n q
  end.
\end{coqcode}

The keyword \cinl{Fail} indicates that the definition was not accepted by Coq without an error.
Here, the command has failed with the error message \texttt{Cannot guess decreasing argument of fix}.
If we want to define such a recursive function that does not follow Coq's restriction, we can use the keyword \cinl{Program Fixpoint} and prove the termination of the function afterwards.
In this thesis, we will not use capabilities like \cinl{Program Fixpoint} to define recursive functions that do not obey Coq's restrictions, so we abstain from going into more details concerning proving termination of recursive functions.

Moving on with our definitions on Peano numbers above, we can introduce a \cinl{Notation} that allows us to write more natural expressions like \cinl{p1 + p2} for the expression \cinl{(add p1 p2)}.
We also provide an associativity for the newly introduced operator syntax as well as a fixity level.

\begin{coqcode}
Notation "p1 + p2" := (add p1 p2) (left associativity, at level 50).
\end{coqcode}

In Coq we can check types, print definitions and evaluate expressions directly in the Coq\--file using so\--called \emph{Vernacular}\--commands like \texttt{Check}, \texttt{Print} and \texttt{Compute} that will print the information on the console.
Since we are used to having a REPL from languages like Haskell and Curry, we will write these commands and the printed answer in a REPL\--style as follows.

\begin{crepl}
\coqrepl Compute (s z + s z).
   = s (s z)
   : nat
\end{crepl}

Let us now consider a polymorphic data type definition as well as polymorphic functions using lists as example.
We use the predefined definition for lists defined as follows.

\begin{coqcode}
Inductive list (A : Type) : Type :=
| nil  : list A
| cons : A -> list A -> list A.
\end{coqcode}

As a code convention we use upper\--case letters for type variables.
In case of polymorphic functions, including constructors of datas types, we need to pass the instantiation of the type arguments explicitly.
For example, when we try to compute the following expression to normal form, Coq yields an error message.

\begin{crepl}
\coqrepl Compute (cons z nil).
Error: The term "z" has type "nat" while it is expected to have type
       "Type".
\end{crepl}

When using Coq, constructors of polymorphic functions need to be applied to more arguments than we are used to from functional languages such as Haskell.
More precisely, all constructors of a polymorphic data type --- like \cinl{list} --- have additional type arguments.
In our case, \cinl{cons} and \cinl{nil} have the following types.

\begin{crepl}
\coqrepl Check nil.
   nil : \(\forall\) (A : Type), list A
\coqrepl Check cons.
   cons : \(\forall\) (A : Type), A -> list A -> list A
\end{crepl}

The first argument is the type that determines the concrete type instantiation of the constructors \cinl{cons} and \cinl{nil}.
The definition above works when we apply a type like \cinl{nat} explicitly or instruct Coq to infer the argument by using an underscore (\cinl{_}).

\begin{crepl}
\coqrepl Compute (cons nat z (nil nat)).
    = cons nat z (nil nat)
    : list nat

\coqrepl Compute (cons _ z (nil _)).
    = cons nat z (nil nat)
    : list nat

\coqrepl Compute (cons _ tt (nil _)).
    = cons unit tt (nil unit)
    : list unit
\end{crepl}

Note that the type \cinl{unit} we use in the third example has only one constructor \cinl{tt}.
Instead of applying type arguments explicitly, we can tweak some settings in order to use functions as we are used to in Haskell, such that type arguments are inferred. \footnote{In particular, we use the option \cinl{Set Implicit Arguments} and specific commands like \cinl{Arguments nil [_]} to make Coq infer all type arguments if possible.}

Next, we define the recursive functions \cinl{length} and \cinl{map} as exemplary functions on lists.

\begin{coqcode}
Fixpoint length (A : Type) (xs : list A) : nat :=
  match xs with
  | nil       => z
  | cons _ ys => s (length ys)
  end.

Fixpoint map (A B : Type) (f : A -> B) (xs : list A) : list B :=
  match xs with
  | nil => nil
  | cons y ys => cons (f y) (map f ys)
  end.
\end{coqcode}

A rather obvious property of a combination of these function states that mapping over a list does not change its length.
In Coq we can define such a proposition mostly consisting of language features we have used so far.

\begin{coqcode}
Lemma map_length (A B : Type) (f : A -> B) (xs : list A)
  : length xs = length (map f xs).
\end{coqcode}

First, instead of using the \cinl{Definition} or \cinl{Fixpoint} keyword, we use \cinl{Lemma} that has the same purpose as the former.
That is, the definition \cinl{map_length} is a function.
While the arguments of the function \cinl{map_length} look as usual, the resulting type involves a dependent type.
The symbol \cinl{=} on the right\--hand side of the type signature is just a notation for the type \cinl{eq} that is defined as follows.

\begin{coqcode}
Inductive eq (A : Type) (x : A) : A -> Prop :=
| eq_refl : eq x x.
\end{coqcode}

The data type \cinl{eq} is a propositional type, indicated by the resulting type \cinl{Prop}.
Intuitively, Coq distinguishes between ordinary programs that are of type \cinl{Type} while proofs are of type \cinl{Prop}.
Furthermore, the type of the constructor \cinl{eq_refl} that represents the reflexivity property for structural equality is \cinl{eq x x}.
Here, \cinl{eq} is a dependent type: the type of the constructor \cinl{eq_refl} uses not only types but the value \cinl{x} in its type signature.
The type \cinl{eq} can only be instantiated with arguments \cinl{x} and \cinl{y} if they evaluate to the same value.
That is, a value of type \cinl{eq} is the proof that two expressions are structurally equal.

The type of lemmas like \cinl{map_length} corresponds to the proposition we want to prove and the implementation is one concrete proof.
That is, if we can implement \cinl{map_length} with type \cinl{length xs = length (map f xs)}, we have proven the corresponding proposition.

\subsection{Proving in Coq: A Step\--by\--Step Introduction}
For the proof of the proposition \cyinl{map_length} we need to take a look at the tactic language \cinl{Ltac}.
We enclose a proof using the commands \cinl{Proof} and \cinl{Qed} and write all tactics we want to apply between these commands.
So, let us start with the proof; we present the tactic or other commands on the left\--hand side and illustrate the progress after that command in a verbatim environment on the right\--hand side.

\begin{cproof}{Proof.}
  A : Type
  B : Type
  f : A -> B
  xs : list A
  ============================
  length xs = length (map f xs)
\end{cproof}

The current state of the proof shows that we have the types \cinl{A} and \cinl{B} in scope as well as a function \cinl{f : A -> B} and a list \cinl{xs : list A}.
These variables are \emph{hypotheses} we can use for the proof.
Underneath the hypotheses we see the resulting type of \cinl{map_length} that represents the current \emph{goal}.
We can then use constructs of the tactic language to manipulate hypotheses and the goal in a sensible way until we find the final proof of the goal.
As we proceed with the proof, we will use \cinl{...} to indicate that we do not list all hypotheses, e.g., we will leave out types of identifiers, variables and hypotheses that we not explicitly use in the current goal.

Taking a look at the goal, we realise that we cannot simplify neither the left\--hand side of the equation nor the right\--hand side, because the definition of \cinl{length} distinguishes between the \cinl{nil} and \cinl{cons} case.
In order to argue about these two different cases separately, we start by doing an induction on \cinl{xs}.
Using the \cinl{induction}\--tactic on a list generates two new goals: one for \cinl{nil} and one for \cinl{cons}.

\begin{cproof}{induction xs as [ | y ys H ].}
  ...
  f : A -> B
  ============================
  length nil = length (map f nil)

  ...
  H : length ys = length (map f ys)
  ============================
  length (cons y ys) =
  length (map f (cons y ys))
\end{cproof}

We use a more involved version of the \cinl{induction}\--tactic that additionally supplies the names for the new arguments that need to be introduced.
The introduction pattern \cinl{[ | y ys H ]} describes the naming conventions for the variables introduced in the two subgoals.
We do not supply any variable names for the first goal, because the \cinl{nil}\--case does not introduce new variables.
For the \cinl{cons}\--subgoal we pass the three names: \cinl{y} and \cinl{ys} for the two arguments of the \cinl{cons}\--constructor as well as \cinl{H} for the induction hypothesis that Coq generates automatically based on the definition of the \cinl{list} data type.
More generally, the vertical bar is a separator for the resulting
subgoals and we introduce new names depending on the arguments of the
corresponding subgoal.
Coq generates names for all arguments that are not explicitly introduced by the pattern.
In case of \cinl{nil}, we can simplify both sides of the equation using the definition of \cinl{length} and \cinl{map} by applying the tactic \cinl{simpl}.

\begin{cproof}{- simpl.}
  ...
  ============================
  z = z
\end{cproof}

We use bullet points like \cinl{-}, \cinl{+}, \cinl{*} to structure the proof and bring the next subgoal in focus.
Now we are at the point that we can use the tactic \cinl{reflexivity}, which, intuitively, constructs the final expression using the above introduced constructor \cinl{eq_refl}.
Next, we take a look at the \cinl{cons}\--case after simplifying both expressions again.

\begin{cproof}{- simpl.}
  ...
  H : length ys = length (map f ys)
  ============================
  s (length ys) = s (length (map f ys))
\end{cproof}

The induction hypothesis \cinl{H} states that the proposition already holds in case of \cinl{ys}.
That is, we can directly rewrite \cinl{length ys} on the left\--hand side of the equation in the goal with the right\--hand side of the hypothesis.

\begin{cproof}{rewrite -> H.}
  ...
  ============================
  s (length (map f ys)) =
  s (length (map f ys))
\end{cproof}

The \cinl{rewrite}\--tactic gets two arguments: the first argument specifies the direction we want to perform the rewriting in\footnote{The direction from left\--to\--right is the default and does not need to be provided in this case.} and the second argument indicates which equality hypothesis we want to rewrite in our goal.
After rewriting the hypothesis, the final goal can be proven using \cinl{reflexivity} again.
We then finish the proof by using the keyword \cinl{Qed}.

\begin{cproof}{Qed.}
  map_length is defined
\end{cproof}

At that point, Coq provides the information that the lemma \cinl{map_length} was successfully defined and can be used in future proofs and definitions.
In order to give a better overview of the proof in its entirety, we restate the proposition as lemma including the complete proof script.

\begin{coqcode}
Lemma map_length (A B : Type) (f : A -> B) (xs : list A)
  : length xs = length (map f xs).
Proof.
  induction xs as [ | y ys H ].
  - simpl. reflexivity.
  - simpl. rewrite -> H. reflexivity.
Qed.
\end{coqcode}

\subsubsection{Induction Principle}
\label{subsub:induction}

In the preceding section we proved a lemma about \cinl{map} and \cinl{length} using induction on the list argument.
The \cinl{induction}\--tactic makes use of the associated induction principle for the corresponding type.
An induction principle for a data type is an ordinary function of type \cinl{Prop}.
Moreover, Coq automatically generates this induction principle for each data type declaration defined using \cinl{Inductive}.
The induction principle for a inductive type \cinl{T} is available as a function named \cinl{T_ind}.

Let us take a look at an example: the induction principle for Coq's predefined list datatype that we used in the last section.

\begin{crepl}
\coqrepl Check list_ind.
  list_ind : \(\forall\) (A : Type) (P : list A -> Prop),
    P nil ->
    (\(\forall\) (e : A) (l : list A), P l -> P (cons e l)) ->
    \(\forall\) (l : list A), P xs
\end{crepl}

Dissecting this type signature gives us the following parts: given a proposition \cinl{P} on lists, we need to supply two proofs.
A proof of type \cinl{P nil} in case of an empty list and a proof of type \cinl{P (cons e l)} otherwise, where \cinl{e} and \cinl{l} are universally quantified.
The first proof is the base case of the induction principle for lists, while the second proof is the inductive case.
That is, in the second proof we assume that the proposition already holds for an arbitrary but fixed list \cinl{l} and can then prove that the proposition also holds for \cinl{cons e l}.

As an example, we reprove the lemma above: \cinl{map} retains the length of its input list.
Instead of using the dedicated \cinl{induction}\--tactic, we use the induction principle directly.
We adapt the above proof as follows.

\begin{coqcode}
Lemma map_length' (A B : Type) (f : A -> B) (xs : list A)
  : length xs = length (map f xs).
Proof.
  apply list_ind with (l := xs).
  - simpl. reflexivity.
  - intros y ys H. simpl. rewrite -> H; reflexivity.
Qed.
\end{coqcode}

When using backward\--reasoning, we have to use the function \cinl{list_ind} on a function of type \cinl{l -> P  l}, which is the type of our lemma, when we instantiate the argument list \cinl{l} with the concrete list \cinl{xs}.
Since \cinl{list_ind} has three arguments, these arguments are now the new subgoals we need to prove.
One of these arguments is the proposition \cinl{P : list A -> Prop} of the induction principle, which Coq infers automatically from the goal we apply the function to.
In this case, the proposition is the function \cinl{fun xs => length xs = length (map f xs)}.
That is, we end up with only two subgoals: we need to prove that the proposition holds for an empty and a non\--empty list.
We can prove the first goal for the \cinl{nil}\--constructor using the same tactics as before: after simplification of the involved functions, both sides of the equation are already equal.
The second goal looks as follows.

\begin{coqcode}
  !$\forall$! (y : A) (ys : list A),
    length ys = length (map f ys) ->
    length (cons y ys) = length (map f (cons y ys))
\end{coqcode}

Given an element \cinl{y},  a list \cinl{ys}, and the induction hypothesis that the proposition already holds for that \cinl{ys}, we need to prove that the proposition holds for \cinl{cons y ys} as well.
While Coq's \cinl{induction} tactic introduces these universally quantified variables for us, the direct variant is not as convenient.
That is, we need to introduce these variables ourselves using \cinl{intros} to specify names for the quantified variables first, but can then prove the goal using the induction hypothesis like in the first version above.

\subsection{Representing Data Types using Containers}
\label{sub:container}

Up to now, the only dependent type we have seen is the equality type we use to state properties for our programs.
Another prominent example for the usage of dependent types originates from generic programming \citep{altenkirch2003generic,hinze2000new}: we can encode a variety of polymorphic data types using containers as introduced by \citet{abbott2003categories}.

A container is described as a product of shapes and a position function.
The shape is a type \cinl{Shape} and the position type \cinl{Pos} is a type function that maps shapes to types.
Using these two components we can define a container extension that gives access to values of type \cinl{A}.

\begin{coqcode}
Inductive Ext (Shape : Type) (Pos : Shape -> Type) (A : Type) : Type :=
| ext : !$\forall$! s, (Pos s -> A) -> Ext Shape Pos A.
\end{coqcode}

A container extension \cinl{Ext Shape Pos} is then isomorphic to a type constructor \cinl{F}, where \cinl{F A} represents the polymorphic data type.
The position type \cinl{Pos} is a dependent type: the type depends on a value of type \cinl{Shape}.
The general idea is that the position type specialised to a shape \cinl{Pos s} describes all the possible positions of a data structure.
A container extension of type \cinl{Ext Shape Pos A} consists of a function \cinl{Pos s -> A} for all shapes \cinl{s : Shape} that gives access to the polymorphic components of the data type it describes.
That is, given one concrete position of type \cinl{Pos s} for a shape \cinl{s}, one value of type \cinl{A} can be accessed.

As an example, let us consider the polymorphic data type \cinl{One} we discussed in \autoref{subsec:freeMonad} in the context of free monads.
Recall that the definition was used by \citet{swierstra2008data} and that the name captures the number of representable values quite well.

\begin{coqcode}
Inductive One (A : Type) : Type :=
| one : One A.
\end{coqcode}

Now we want to represent \cinl{One} as a container described as a type of shapes and a position type function.
The data type \cinl{One} has only one constructor, that is, there is only one shape that we need to represent.

\begin{coqcode}
Definition One!$_\text{S}$! := unit.
\end{coqcode}

Intuitively, the shape type represents the different constructors of a data type.
Note that instead of introducing a new type with one value, we reuse Coq's \cinl{unit} type.
The only value of type \cinl{unit} is called \cinl{tt}.

The position type function, on the other hand, describes the possible positions of polymorphic arguments for a given constructor.
As observed above, \cinl{One} has only one constructor.
This constructor \cinl{one} does not have any polymorphic arguments.
More precisely, there are no possible positions for polymorphic argument for any constructor.
In order to represent that there are no possible positions, we use an empty type.
An empty type is a data type without any values, that is, we cannot construct values of that type.

\begin{coqcode}
Inductive Empty : Type := .
Definition One!$_\text{P}$! (s : One!$_\text{S}$!) := Empty.
\end{coqcode}
\label{code:one_container}

Recall that the position type function depends on the corresponding shape.
Here, however, the shape does not matter as we do not have any position anyhow.

Using Coq's ability to prove properties about programs, we can show that the container representation is isomorphic to the original data type.
More precisely, we define two functions \cinl{from_One} and \cinl{to_One}, and show that both compositions yield the identity.

\begin{coqcode}
Definition from_One A (o : One A) : Ext One!$_\text{S}$! One!$_\text{P}$! A :=
  ext tt (fun (p : One!$_\text{P}$! tt) => match p with end).

Definition to_One A (e : Ext One!$_\text{S}$! One!$_\text{P}$! A) : One A :=
  one.
\end{coqcode}

The definition of \cinl{from_One} uses an empty pattern match, because there are no possible values of type \cinl{One!$_{\text{P}}$! tt} --- recall that the type is just a synonym for \cinl{Empty}.
Since the argument \cinl{p} is of type \cinl{Empty} and \cinl{Empty} has no constructors to match on, Coq realises that we cannot have a value of type \cinl{Empty} in the first place and accepts the definition.
That is, we can define a function that yields an arbitrary polymorphic value given an empty type as argument.

\begin{coqcode}
Definition bogus (A : Type) (em : Empty) : A :=
  match em with end.
\end{coqcode}

The definition of \cinl{to_One}, on the other hand, is straightforward, because there is only one way to construct the value \cinl{one}.
Note that we do not need to access a polymorphic component of type \cinl{A} in order to construct a value of type \cinl{One}.

An alternative definition of the above function displays the components of the argument type \cinl{Ext One!$_{\text{S}}$! One!$_{\text{P}}$! A} more explicitly.

\begin{coqcode}
Definition to_One A (e : Ext One!$_\text{S}$! One!$_\text{P}$! A) : One A :=
  let '(ext tt pf) := e in one.
\end{coqcode}

Since \cinl{Ext} has only one constructor, Coq allows a let\--binding for such irrefutable patterns with the above syntax using a tick \cinl{'}.
The function \cinl{pf} is of type \cinl{One!$_\text{P}$! tt -> A}, which becomes a function of type \cinl{Empty -> A} after inlining the definition of \cinl{One!$_\text{P}$! tt}.
As a side note, we could not produce a value of type \cinl{A} using the function \cinl{pf} if we needed to: we cannot construct the appropriate argument of type \cinl{Empty}.

With these definitions at hand, we can prove that \cinl{from_One} and \cinl{to_One} form an isomorphism.
We start with the more involved proof \cinl{from_to_One} first.

\begin{coqcode}
Lemma from_to_One : !$\forall$! (A : Type) (e : Ext One!$_\text{S}$! One!$_\text{P}$! A),
    from_One (to_One e) = e.
\end{coqcode}

\begin{cproof1}{Proof. intros A e; simpl.}{0.4}{0.6}
  e : Ext One\(\sb{\text{S}}\) One\(\sb{\text{P}}\) A
  ============================
  ext tt (fun p => match p with end) = e
\end{cproof1}

In contrast to the definition of \cinl{map_length}, we introduce all variables on the right\--hand side of the type signature.
After introducing these quantified variables as hypotheses, we simplify the function applications of \cinl{from_One} and \cinl{to_One} as they disregard the given argument.
Next, we observe that the argument \cinl{e} on the right\--hand side of the equation is of type\cinl{Ext}: it must be constructed using \cinl{ext} as well.
More precisely, we even know that the first component, the shape, is of type \cinl{One!$_\text{S}$!}, and, hence, the only valid value is \cinl{tt}.
We destruct \cinl{e} directly in its two components: \cinl{tt} for the shape and \cinl{pf} for the position function using \cinl{[]} as a nested destruction for the shape.
The pattern \cinl{[]} can be used for deconstruction of values with only one constructors and without any arguments.

\begin{cproof1}{destruct e as [ [] pf ].}{0.4}{0.6}
  s : One\(\sb{\text{S}}\)
  pf : One\(\sb{\text{P}}\) s -> A
  ============================
  ext tt (fun p => match p with end) =
  ext tt pf
\end{cproof1}

Next, we observe that we have the \cinl{ext}\--constructor on both sides of the equation.
By applying the tactic \cinl{f_equal}, we use the following predefined lemma about functional equality.

\begin{crepl}
\coqrepl Check f_equal.
  f_equal : \(\forall\) (A B : Type) (f : A -> B) (x y : A), x = y -> f x = f y
\end{crepl}

In the case above, we apply the function \cinl{ext tt} to \cinl{(fun p => match p with end)} on the left\--hand side and to \cinl{pf} on the right\--hand side of the equation.
 
\begin{cproof1}{f_equal.}{0.3}{0.7}
  pf : One\(\sb{\text{P}}\) tt -> A
  ============================
  (fun p : One\(\sb{\text{P}}\) tt => match p with end) = pf
\end{cproof1}

The assumption of the \cinl{f_equal}\--lemma becomes the new goal: we now need to show that the two position functions are the same.
In pure functional programming we are used to a property called functional extensionality: two functions \cinl{f : A -> B} and \cinl{g : A -> B} are equal, if they yield the same result for all possible input arguments of appropriate type.

\begin{crepl}
\coqrepl Check  functional_extensionality.
  functional_extensionality : \(\forall\) (A B : Type) (f g : A -> B),
    (\(\forall\) (x : A), f x = g x) -> f = g
\end{crepl}

The corresponding tactic \cinl{extensionality p} introduces a new variable \cinl{p} of appropriate type that is used as argument on both sides of the equation.

\begin{cproof}{extensionality p.}
  pf : One\(\sb{\text{P}}\) tt -> A
  p : One\(\sb{\text{P}}\) tt
  ============================
  match p with end = pf p
\end{cproof}

Finally, we prove the statement by realising that the newly introduced argument \cinl{p} of type \cinl{One!$_\text{P}$! tt} is a value of an empty type.
Recall that \cinl{One!$_\text{P}$! tt} can be inlined to \cinl{Empty}.
That is, we finish the proof by trying to destruct the value \cinl{p} into its corresponding constructors.
Coq realises that there are no values of that type, which proves the last goal.

\begin{cproof}{destruct p. Qed.}
  from_to_One is defined
\end{cproof}

The second direction of the isomorphism proof is straightforward.
After introducing all variables and destructing the argument \cinl{o : One A}, we simplify the left\--hand side of the equation using the function definitions of \cinl{from_One} and \cinl{to_One}.
The simplification already leads to the value \cinl{one : One A} on the left\--hand side as well as on the right\--hand side.

\begin{coqcode}
Lemma to_from_One : !$\forall$! (A : Type) (o : One A),
    to_One (from_One o) = o.
Proof.
  intros A o; destruct o; reflexivity.
Qed.
\end{coqcode}
